model cessna_digital_twin

use Mars

layer AgentLayer as agentlayer
{	
	//Weather Values TODO: Think about another solution for weather
	var Weather__wind_bearing : real = 107 // ° range 0..360
	var Weather__wind_speed : real = 5 // m/s
	def Get_Weather__wind_bearing() => return Weather__wind_bearing
	def Get_Weather__wind_speed() => return Weather__wind_speed
	
	//help variables for density calculations
	var density_zero : real = 1.225 // kg/m^3 - mean sea level ISA
	var temperature_zero : real = 288.15 // Kelvin - mean sea level ISA
	var L_constant : real = 0.0065 // constant for dT/dh
		
	def Get_Weather__density(height : real)
	{
		var Weather__density : real
		Weather__density = density_zero * (1 - (L_constant * height / temperature_zero))**4.2561
		return Weather__density
	}

	
	// Physics Values
	var gravity : real = 9.81 // m/s^2
	def Get_gravity() => return gravity
	
	// Callsign Handling
	var callsign_number : integer = 0
	
	def Get_callsign_number()
	{
		callsign_number = callsign_number + 1
		return callsign_number
	}
	
	//-----Communication Handling-----
	var frequency_blocked = false
	var sender_identifier = ""
	var receiver = ""
	var message_type = ""
	// either information is at first place "0" or contains multiple information as e.g. flight_path
	var message_information_path : List<Tuple<real, real>>
	var message_information_heading : real
	var message_information_bool : bool
	
	
	def Listen_receiver_on_frequency() => return receiver
	def Listen_sender_identifier_on_frequency() => return sender_identifier
	def Listen_message_type_on_frequency() => return message_type
	def Listen_message_information_path_on_frequency() => return message_information_path 
	def Listen_message_information_heading() => return message_information_heading
	def Listen_message_information_bool()=> return message_information_bool
	
	
	def Communicate_answer_on_frequency(_sender_identifier : string, _receiver : string, _message_type : string, _message_information_path : List<Tuple<real, real>>, _message_information_heading : real, _message_information_bool : bool)
	{
		if (frequency_blocked === false or _sender_identifier ==="Tower")
		{
			sender_identifier = _sender_identifier
			receiver = _receiver
			message_type = _message_type
			message_information_path  = _message_information_path 
			message_information_heading = _message_information_heading
			message_information_bool = _message_information_bool
			frequency_blocked = true
		}		
	}
	
	def Communicate_request_on_frequency(_sender_identifier : string, _receiver : string, _message_type : string)
	{
		if (frequency_blocked === false)
		{
			sender_identifier = _sender_identifier
			receiver = _receiver
			message_type = _message_type
			
			frequency_blocked = true
		}		
	}
	
	def Clear_frequency()
	{
		frequency_blocked = false
		sender_identifier = ""
		receiver = ""
		message_type = ""
		message_information_path = #[#(0.0, 0.0)]
	}
	
}

//TODO: Not used so far
vector-layer AirportStadeLayer as airportstadelayer

// -----Agent Observer for spawning sequence-----
agent Observer on AgentLayer{
	var state_list = new List<string>
	var formula = new Formula()
	
	//-----Spawn Handling-----
	// Bottom Left of appron field
	var left_xcor : real = 9.4987928
	var left_ycor : real = 53.560392
	var right_xcor : real = 9.5009108
	var right_ycor : real = 53.5599218
	
	var number_of_spawning_points : integer = 8
	
	var spawn_xcor_list = new List<real>()
	var spawn_ycor_list = new List<real>()
	
	var spawn_location_number : integer
	
def initialize_spawn_cor()
	{
		var temp_deltastep_xcor = (left_xcor - right_xcor) / (number_of_spawning_points-1)
		var temp_deltastep_ycor = (left_ycor - right_ycor) / (number_of_spawning_points-1)
		for (var i = 0; i < number_of_spawning_points; i++)
		{
			spawn_xcor_list.Add(left_xcor - temp_deltastep_xcor * i)
			spawn_ycor_list.Add(left_ycor - temp_deltastep_ycor * i)
		}
		spawn_location_number = 0
	}
	
def spawn_pilot_and_aircraft()
	{
		
		
		// only spawn, when no aircraft is nearby, else go to next spawn location and try again until tried at all location
		var spawn_successful = false
		var i = 0
		while (i < number_of_spawning_points and spawn_successful === false)
		{
			i = i + 1
			var spawn_cor = #(spawn_xcor_list.Get(spawn_location_number), spawn_ycor_list.Get(spawn_location_number))
			var aircraft_array = explore Aircraft where [x => return formula.haversine(x.Get_position(), spawn_cor) < 10]
			if (length(aircraft_array) === 0)
			{
				// spawn order is important, since pilot needs to find an aircraft! 
				spawn Aircraft at spawn_cor
				spawn Pilot at spawn_cor
				spawn_successful = true
			}
			spawn_location_number = spawn_location_number + 1
			
			if (spawn_location_number >= number_of_spawning_points)
			{
				spawn_location_number = 0
			}
		}
	}

	initialize{
		state_list = #["PreflightInspection", "StartingEngine", "TakeOffPreparationRequest", "Taxiing", "TakeOffPreparation", "TakeOffHoldShortRequest", "TakeOffRequest", "TakeOff", "LeavingAirspaceRequest"]
		initialize_spawn_cor()
	}
	
	tick
	{	
		if (simtime % 150 === 0)
		{
			println "Simulation Time: " + simtime
			println "Number of Pilots in each State"
			each (var state in state_list)
			{
				var pilot_array = explore Pilot where [x => return x.Get_state() === state]
				println state + ": " + length(pilot_array)
			}
			println "-----End of information-----"
		}
		
		if (simtime === 1 or simtime === 5 or simtime % 50 === 0) // add for multiple spawns: or simtime % 100 === 0
		{
			spawn_pilot_and_aircraft()
		}
	}
}

// -----Agent Pilot flying the aircraft-----
agent Pilot on AgentLayer{
		
	initialize
	{	
		//General Initialization
		myAircraft = nearest Aircraft where [it => return it.Get_occupy_bool() === false]
		myAircraft.Set_occupied()		
		myAircraft_callsign = myAircraft.Get_callsign() // save aircraft callsign --> IDENTIFIER!
		update_general_values() // so first row of csv is not empty
		state = "PreflightInspection" // always first state so far
		current_activity = "Initialization:)"
		event_info = "None"
		
		// Pilot attributes and share them with classes
		age = age_min + random((age_max - age_min) + 1)
		flight_experience = random(age - age_min) +1
		timehandler.initialize_variables(age, flight_experience, age_max, age_min)
	}
		
	tick
	{	
		if (state === "PreflightInspection"){
			PreflightInspection_action()
		}
		
		else if (state === "StartingEngine"){
			StartingEngine_action()
		}
		
		else if (state === "TakeOffPreparationRequest"){
			TakeOffPreparationRequest_action()
		}
		
		else if (state === "Taxiing"){
			Taxiing_action()
		}
		
		else if (state === "TakeOffPreparation"){
			TakeOffPreparation_action()
		}
		
		else if (state === "TakeOffHoldShortRequest"){
			TakeOffHoldShortRequest_action()
		}
		
		else if (state === "TakeOffRequest"){
			TakeOffRequest_action()
		}
		
		else if (state === "TakeOff"){
			TakeOff_action()
		}
		
		else if (state === "LeavingAirspaceRequest"){
			LeavingAirpsaceRequest_action()
		}
		
		
		update_general_values()
	}
	
	
	//-----General Values and functions (Settings)-----
	observe var Latitude : real
	observe var Longitude : real
	observe var myAircraft_callsign : string
	observe var state : string
	observe var current_activity : string
	observe var event_info : string
	
	//Classes and Agents
	var myAircraft : Aircraft // for accessing the real aircraft
	var timehandler = new TimeHandler()
	var formula = new Formula()
	
	//help variables
	var temp_throttle_value : real // for throttle management between 0...1
	var temp_pitch_value : real // for pitch management
	var next_action : string
	var first_action_set : bool = false // temporary auxiliary variable for first action in each state
	var taxi_path : List<Tuple<real, real>> // for saving taxi instructions
	var active_taxi_point_number : integer = 0 // for indicating actual taxi point
	var heading_information : real // for saving heading value
	var state_after_taxiing : string
	var distance_to_next_point : real
	var first_fixing_attempt : bool = true
	var V_rotate : real = 30
	
	def go_to_next_state(_state : string)
	{
		state = _state
		first_action_set = false
		if (state === "Taxiing")
		{
			//reset placed here
			active_taxi_point_number = 0
		}
	}
	
	def remove_me_and_my_aircraft(info : string)
	{
		event_info = "End flight mission due to " + info
		myAircraft.Remove()
		kill me
	}
	
	passive Removed_with_my_aircraft(info : string) //
	{
		event_info = "End flight mission due to " + info
		myAircraft.Remove()
		kill me
	}
	
	def update_general_values()
	{
		pos at(myAircraft.Get_position())
		Longitude = xcor
		Latitude = ycor
		current_activity = next_action
	}
	passive Get_state() => return state  // for observer agent
	
	//-----General Pilot Values and functions-----
	observe var age: integer // years
	observe var flight_experience : integer // years
		
	//Settings
	var age_max : integer = 75
	var age_min : integer = 18
	var flight_experience_max : integer = 57
	
	
	//-----State Preflight Inspection-----
	
	//help function:
	def skip_action(base_probability: real, action_name : string) : bool
	{
		// the probability of skiping repair action increases with flight experience
		var random_value = random(10**6)/10**6
		var threshold_value = base_probability + flight_experience/flight_experience_max * 0.1
		if (random_value >= threshold_value)
		{
			return false
		}
		else
		{
			event_info = "Skipped " + action_name
			return true
		}
	}
	
	def PreflightInspection_action()
	{
		// Flag for first action and architecture like this avoids loose of one tick
		if (first_action_set === false)
		{
			next_action = "Check_TireRightMainWheel__inflation"
			first_action_set = true
		}
		
		if (next_action === "Check_TireRightMainWheel__inflation")
		{
			timehandler.create_action_duration(10, 10, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_TireRightMainWheel__inflation()
				if (check_value >= 0)
				{
					next_action = "Check_RWT__water_sediments"
				}
			}	
		}		
		
		else if (next_action === "Check_RWT__water_sediments")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_RWT__water_sediments()
				next_action = "Check_RWT__fuel_quantity"
				
				if (check_value === true)
				{
					if(skip_action(0.05, "Repair_RWT__water_sediments") === false)
					{
						next_action = "Repair_RWT__water_sediments"
					}
					
				}
			}
		}
		
		else if(next_action === "Repair_RWT__water_sediments")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Repair_Visual_RWT__water_sediments()
				next_action = "Check_RWT__fuel_quantity"
			}
		}
		
		else if (next_action === "Check_RWT__fuel_quantity")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_RWT__fuel_quantity()
				if (check_value >= 0) 
				{
					next_action = "Check_Engine__oil"
				}
			}
		}
				
		else if (next_action === "Check_Engine__oil")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_Engine__oil()
				next_action = "Check_TireNoseWheel__inflation"
				
				if (check_value < myAircraft.Get_Engine__oil_min())
				{
					if (skip_action(0.05, "Refill_Engine__oil") === false)
					{
						next_action = "Refill_Engine__oil"
					}					
				}
			}
		}
		
		else if(next_action === "Refill_Engine__oil")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Refill_Visual_Engine__oil()
				next_action = "Check_TireNoseWheel__inflation"
			}
		}
		
		else if (next_action === "Check_TireNoseWheel__inflation")
		{
			timehandler.create_action_duration(10, 10, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_TireNoseWheel__inflation()
				if (check_value >= 0) 
				{
					next_action = "Check_TireLeftMainWheel__inflation"
				}
			}
		}
		
		else if (next_action === "Check_TireLeftMainWheel__inflation")
		{
			timehandler.create_action_duration(10, 10, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_TireLeftMainWheel__inflation()
				if (check_value >= 0) 
				{
					next_action = "Check_LWT__water_sediments"
				}
			}
		}				
		
		else if(next_action === "Check_LWT__water_sediments")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_LWT__water_sediments()
				next_action = "Check_LWT__fuel_quantity"
				
				if (check_value === true)
				{
					if(skip_action(0.05, "Repair_LWT__water_sediments") === false)
					{
						next_action = "Repair_LWT__water_sediments"
					}
				}
			}
		}
		
		else if(next_action === "Repair_LWT__water_sediments")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Repair_Visual_LWT__water_sediments()
				next_action = "Check_LWT__fuel_quantity"
			}
		}					
					
		else if (next_action === "Check_LWT__fuel_quantity")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_LWT__fuel_quantity()
				if (check_value >= 0) 
				{
					next_action = "End_of_Actions"
				}
			}
		}
			
		if (next_action === "End_of_Actions")
		{
			go_to_next_state("StartingEngine")
		}
	}
	
	
	
	//-----State Starting Engine-----
	def StartingEngine_action()
	{
		if (first_action_set === false)
		{
			next_action = "Set_Brake__parking_brake"
			first_action_set = true
		}
		
		if (next_action === "Set_Brake__parking_brake")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Brake__parking_brake("SET")
				next_action = "Set_Engine__mixture_control"
			}
		}
		
		else if (next_action === "Set_Engine__mixture_control")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				if(skip_action(0.05, "Set_Engine__mixture_control") === false)
				{
					Set_Engine__mixture_control("RICH") // set to rich
				}
				next_action = "Set_CIP__master_switch"
			}
		}
		
		else if (next_action === "Set_CIP__master_switch")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_CIP__master_switch("ON")
				next_action = "Apply_Engine__throttle"
			}
		}
		
		else if (next_action === "Apply_Engine__throttle")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				temp_throttle_value = 0.10 // idle throttle
				Apply_Engine__throttle(temp_throttle_value)
				next_action = "Set_Engine__ignition_switch_START"
			}
		}
		
		else if (next_action === "Set_Engine__ignition_switch_START")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Engine__ignition_switch("START")
				next_action = "Check_Engine__running"
			}
		}
		
		else if (next_action === "Check_Engine__running")
		{
			timehandler.create_action_duration(2, 1, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_Engine__running()
				if (check_value === true)
				{
					next_action = "Set_Engine__ignition_switch_BOTH"
				}
				else if (check_value === false)
				{
					next_action = "Check_Engine__running"
				}
			}
		}
			
		else if (next_action === "Set_Engine__ignition_switch_BOTH")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Engine__ignition_switch("BOTH")
				next_action = "Check_Instrument_Engine__oil_pressure"
			}
		}
		
		else if (next_action === "Check_Instrument_Engine__oil_pressure")
		{
			timehandler.create_action_duration(30, 2, "pilot_age_and_experience") // includes holding pattern
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				next_action = "Check_Instrument_Engine__oil_temperature"
				if (Check_Instrument_Engine__oil_pressure() < myAircraft.Get_Engine__oil_pressure_normal_min())
				{
					if(skip_action(0.05, "Check_Instrument_Engine__oil_pressure") === false)
					{
						if (first_fixing_attempt === true)
						{
							event_info = "Engine__oil_pressure too low, back to state PreflightInspection"
							go_to_next_state("PreflightInspection")
							first_fixing_attempt = false
							Set_Engine__ignition_switch("OFF")
						}
						else
						{
							remove_me_and_my_aircraft("low engine oil pressure")
						}
					}
				}
			}
		}
		
		else if (next_action === "Check_Instrument_Engine__oil_temperature")
		{
			timehandler.create_action_duration(3, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				next_action = "End_of_Actions"
				if (Check_Instrument_Engine__oil_temperature() > myAircraft.Get_Engine__oil_temperature_normal_max())
				{
					if(skip_action(0.05, "Check_Instrument_Engine__oil_temperature") === false)
					{
						if (first_fixing_attempt === true)
						{
							event_info = "Engine__oil_temperature too high, back to state PreflightInspection"
							go_to_next_state("PreflightInspection")
							first_fixing_attempt = false
							Set_Engine__ignition_switch("OFF")
						}
						else
						{
							remove_me_and_my_aircraft("high engine oil temperature")
						}
					}
				}				
			}
		}
		
		if (next_action === "End_of_Actions")
		{
			go_to_next_state("TakeOffPreparationRequest")
		}
	}
	
	
	//-----State TakeOffPreparationRequest-----
	def TakeOffPreparationRequest_action()
	{
		if (first_action_set === false)
		{
			next_action = "Communicate_on_frequency"
			first_action_set = true
		}
		
		if (next_action === "Communicate_on_frequency")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Communicate_request_on_frequency(myAircraft_callsign, "Tower", "RequestTakeOffPreparationPoint")
				next_action = "Listen_receiver_on_frequency"
			}
		}
		
		else if (next_action === "Listen_receiver_on_frequency")
		{
			var temp_receiver = agentlayer.Listen_receiver_on_frequency()
			if (temp_receiver === myAircraft_callsign)
			{
				taxi_path = agentlayer.Listen_message_information_path_on_frequency()
				next_action = "Clear_frequency"
			}
			
			//retry contacting tower after a specific period of time
			timehandler.create_action_duration(10, 5, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				next_action = "Communicate_on_frequency"
			}
		}
		
		// to make sure that message is not instantly deleted and simulate processing time of incoming message and blockage
		else if (next_action === "Clear_frequency")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Clear_frequency()
				next_action = "End_of_Actions"
			}
		}
		
		if (next_action === "End_of_Actions")
		{
			go_to_next_state("Taxiing")
			state_after_taxiing = "TakeOffPreparation"
		}
	}
	
	
	//-----State TakeOff Preparation
	def TakeOffPreparation_action()
	{
		if (first_action_set === false)
		{
			next_action = "Set_Brake__parking_brake"
			first_action_set = true
		}
		
		if (next_action === "Set_Brake__parking_brake")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Brake__parking_brake("SET")
				next_action = "Check_all_Flight_Instruments"
			}
		}
		
		else if (next_action === "Check_all_Flight_Instruments")
		{
			timehandler.create_action_duration(20, 10, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Check_Instrument_LWT__fuel_quantitity()
				Check_Instrument_RWT__fuel_quantitity()
				Check_Instrument_Aircraft__height()
				Check_Instrument_Aircraft__rate_of_climb()
				Check_Instrument_Aircraft__pitch()
				Check_Instrument_Aircraft__true_air_speed()
				next_action = "Set_Engine__mixture_control"
			}
		}
		
		else if(next_action === "Set_Engine__mixture_control")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				if(skip_action(0.05, "Set_Engine__mixture_control") === false)
				{
					Set_Engine__mixture_control("RICH") // set to rich
				}
				next_action = "Apply_Engine__throttle_to_1700RPM"
				temp_throttle_value = 0.20 //starting point for throttle
			}
		}
		
		else if (next_action === "Apply_Engine__throttle_to_1700RPM")
		{
			//Instructions are to apply throttle, so that rpm equal 1700
			timehandler.create_action_duration(1, 1, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Apply_Engine__throttle(temp_throttle_value)
				if (Check_Instrument_Engine__RPM < 1700)
				{
					temp_throttle_value = temp_throttle_value + 0.05
					next_action = "Apply_Engine__throttle_to_1700RPM"
				}
				else
				{
					next_action = "Check_Engine_Instruments"
				}
			}
		}
		
		else if(next_action === "Check_Engine_Instruments")
		{
			timehandler.create_action_duration(10, 5, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				//usually here should be a magneto with RPM check
				
				if (Check_Instrument_Engine__oil_temperature() > myAircraft.Get_Engine__oil_temperature_normal_max())
				{
					if(skip_action(0.05, "Check_Instrument_Engine__oil_temperature") === false)
					{
						remove_me_and_my_aircraft("high engine oil temperature")
					}
				}
				
				if (Check_Instrument_Engine__oil_pressure() < myAircraft.Get_Engine__oil_pressure_normal_min())
				{
					if(skip_action(0.05, "Check_Instrument_Engine__oil_pressure") === false)
					{
						remove_me_and_my_aircraft("low engine oil pressure")
					}
				}
				
				next_action = "Apply_Engine__throttle_idle"
				temp_throttle_value = 0.10 //idle throttle
			}
		}
		
		else if(next_action === "Apply_Engine__throttle_idle")
		{
			timehandler.create_action_duration(3, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Apply_Engine__throttle(temp_throttle_value)
				next_action = "End_of_Actions"
			}
		}
		
		
		if (next_action === "End_of_Actions")
			{
				go_to_next_state("TakeOffHoldShortRequest")
			}
	}
	
	
	
	//-----State TakeOffHoldShortRequest-----
	def TakeOffHoldShortRequest_action()
	{
		if (first_action_set === false)
		{
			next_action = "Communicate_on_frequency"
			first_action_set = true
		}
		
		if (next_action === "Communicate_on_frequency")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Communicate_request_on_frequency(myAircraft_callsign, "Tower", "RequestTakeOffHoldShortPoint")
				next_action = "Listen_receiver_on_frequency"
			}
		}
		
		else if (next_action === "Listen_receiver_on_frequency")
		{
			var temp_receiver = agentlayer.Listen_receiver_on_frequency()
			if (temp_receiver === myAircraft_callsign)
			{
				taxi_path = agentlayer.Listen_message_information_path_on_frequency()
				next_action = "Clear_frequency"
			}
			
			//retry contacting tower after a specific period of time
			timehandler.create_action_duration(10, 5, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				next_action = "Communicate_on_frequency"
			}
		}
		
		// to make sure that message is not instantly deleted and simulate processing time of incoming message and blockage
		else if (next_action === "Clear_frequency")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Clear_frequency()
				next_action = "End_of_Actions"
			}
		}
		
		if (next_action === "End_of_Actions")
		{
			go_to_next_state("Taxiing")
			state_after_taxiing = "TakeOffRequest"
		}
	}
	
	
	
	//-----State TakeOffHoldShortRequest-----
	def TakeOffRequest_action()
	{
		if (first_action_set === false)
		{
			next_action = "Communicate_on_frequency"
			first_action_set = true
		}
		
		if (next_action === "Communicate_on_frequency")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Communicate_request_on_frequency(myAircraft_callsign, "Tower", "RequestTakeOff")
				next_action = "Listen_receiver_on_frequency"
			}
		}
		
		else if (next_action === "Listen_receiver_on_frequency")
		{
			var temp_receiver = agentlayer.Listen_receiver_on_frequency()
			if (temp_receiver === myAircraft_callsign)
			{
				taxi_path = agentlayer.Listen_message_information_path_on_frequency()
				heading_information = agentlayer.Listen_message_information_heading()
				next_action = "Clear_frequency"
			}
			
			//retry contacting tower after a specific period of time
			timehandler.create_action_duration(10, 5, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				next_action = "Communicate_on_frequency"
			}
		}
		
		// to make sure that message is not instantly deleted and simulate processing time of incoming message and blockage
		else if (next_action === "Clear_frequency")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Clear_frequency()
				next_action = "End_of_Actions"
			}
		}
		
		if (next_action === "End_of_Actions")
		{
			go_to_next_state("Taxiing")
			state_after_taxiing = "TakeOff"
		}
	}
	
	//-----State TakeOff-----
	//TODO: Add Instrument Screening and better mechanism for take off
	def TakeOff_action()
	{
		if (first_action_set === false)
		{
			next_action = "Set_Throttle_and_Heading"
			first_action_set = true
		}
		
		if (next_action === "Set_Throttle_and_Heading")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				// Set Aircraft Heading
				Set_Aircraft__heading_bearing(heading_information)
				Apply_Engine__throttle(1.0)
				Apply_Brake__deceleration(0.0)
				temp_pitch_value = 0
				next_action = "ReadyForRotate"
			}
		}
		
		else if (next_action === "ReadyForRotate")
		{
			timehandler.create_action_duration(1, 1, "pilot_age_and_experience")  // simulating check of instrument
			if (timehandler.hold_action_time(timehandler.action_duration))  
			{
				if (Check_Instrument_Aircraft__true_air_speed() > V_rotate) // TODO: Add calculation of V_rotate
				{
					temp_pitch_value = temp_pitch_value + 2
					Apply_Aircraft__pitch(temp_pitch_value)
					if (temp_pitch_value >= 8)
					{
						next_action = "HoldAircraft"
					}
					
				}
			}
		}
		
		else if (next_action === "HoldAircraft")
		{
			timehandler.create_action_duration(5, 1, "pilot_age_and_experience") 
			if (timehandler.hold_action_time(timehandler.action_duration))  
			{
				if (Check_Instrument_Aircraft__height() > 0)
				{
					next_action = "ControlAircraft"
				}
				else
				{
					temp_pitch_value = 0
					Apply_Aircraft__pitch(temp_pitch_value)
					V_rotate = V_rotate + 5
					next_action = "ReadyForRotate"
				}
			}
		}
		
		else if (next_action === "ControlAircraft")
		{
			timehandler.create_action_duration(3, 1, "pilot_age_and_experience")  // simulating check of instrument every 2 sec
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				if (Hear_Aircraft__stall_sound() === true)
				{
					temp_pitch_value = temp_pitch_value - 1
				}
				else if (Check_Instrument_Aircraft__rate_of_climb() > 3)
				{
					if (Feel_Aircraft__deceleration_z === false)
					{
						temp_pitch_value = temp_pitch_value - 0.5
					}
				}
				else if (Check_Instrument_Aircraft__rate_of_climb() < 1)
				{
					if (Feel_Aircraft__acceleration_z === false)
					{
						temp_pitch_value = temp_pitch_value + 0.5
					}
				}
				Apply_Aircraft__pitch(temp_pitch_value)
				
				if (Check_Instrument_Aircraft__height() > 300)
				{
					next_action = "End_of_Actions"
				}
			}
		}
		
		if (next_action === "End_of_Actions")
		{
			go_to_next_state("LeavingAirspaceRequest")
		}	
		
		
		// TODO: Think about better handling and protocol
		if (myAircraft.Get_Engine__failure() === true)
		{
			remove_me_and_my_aircraft("Engine__Failure during TakeOff")
		}
	}
	
	
	
	//-----State LeavingAirpsaceRequest-----
	def LeavingAirpsaceRequest_action()
	{
		if (first_action_set === false)
		{
			next_action = "Communicate_on_frequency"
			first_action_set = true
		}
		
		if (next_action === "Communicate_on_frequency")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Communicate_request_on_frequency(myAircraft_callsign, "Tower", "RequestLeavingAirpsace")
				next_action = "Listen_receiver_on_frequency"
			}
		}
		
		else if (next_action === "Listen_receiver_on_frequency")
		{
			var temp_receiver = agentlayer.Listen_receiver_on_frequency()
			if (temp_receiver === myAircraft_callsign)
			{
				var temp_bool = agentlayer.Listen_message_information_bool()
				//TODO add waiting mechanism, if answer returns false
				if (temp_bool === true)
				{
					next_action = "Clear_frequency"
				}
			}
			
			//retry contacting tower after a specific period of time
			timehandler.create_action_duration(10, 5, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				next_action = "Communicate_on_frequency"
			}
		}
		
		// to make sure that message is not instantly deleted and simulate processing time of incoming message and blockage
		else if (next_action === "Clear_frequency")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Clear_frequency()
				remove_me_and_my_aircraft("Leaving Airspace")
			}
		}
	}
	
	
	//-----State Taxiing in general-----
	
	// defining help functions
	def Taxiing_to_final_point()  // rolling to the end	
	{
		if (distance_to_next_point < 5)
		{
			temp_throttle_value = 0.1
			Apply_Brake__deceleration(0.5)
			next_action = "End_of_Actions"
		}
		else
		{
			if (Check_Visual_Aircraft__ground_speed_x() > 5)
			{
				temp_throttle_value = 0.1
				Apply_Brake__deceleration(0.2)
			}
			else
			{
				Taxiing_normal()
			}
		}
		Apply_Engine__throttle(temp_throttle_value)
	}
	
	def Taxiing_normal()  // Throttle handling in normal case	
	{
		if (Check_Visual_Aircraft__ground_speed_x() > 6)
		{	
			if (Feel_Aircraft__deceleration_x() === false)
			{
				temp_throttle_value = temp_throttle_value - 0.10
			}
		}
		else if (Check_Visual_Aircraft__ground_speed_x() < 2)
		{
			if (Feel_Aircraft__acceleration_x() === false)
			{
				temp_throttle_value = temp_throttle_value + 0.30
			}
		}
		else if (Check_Visual_Aircraft__ground_speed_x() < 5)
		{
			if (Feel_Aircraft__acceleration_x() === false)
			{
				temp_throttle_value = temp_throttle_value + 0.05
			}
		}
		Apply_Engine__throttle(temp_throttle_value)
	}
	
	def Taxiing_action()
	{
		var taxi_path_points = taxi_path.Size()
		if (first_action_set === false)
		{
			next_action = "Set_Brake__parking_brake"
			first_action_set = true
		}
		
		if (next_action === "Set_Brake__parking_brake")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Brake__parking_brake("OFF")
				Apply_Brake__deceleration(0.0)
				next_action = "Taxiing"
			}
		}
		
		else if (next_action === "Taxiing")
		{
			// Get active Taxi point
			var active_taxi_point = taxi_path.Get(active_taxi_point_number)
			// Set Aircraft Heading
			Set_Aircraft__heading_bearing(formula.bearing(myAircraft.Get_position(), active_taxi_point))
			//Distance and throttle handling
			distance_to_next_point = formula.haversine(myAircraft.Get_position(), active_taxi_point)			
			if (distance_to_next_point < 12)
			{
				//When reaching end point
				if (taxi_path_points === (active_taxi_point_number + 1))
				{
					Taxiing_to_final_point()							
				}
				else
				{
					active_taxi_point_number = active_taxi_point_number + 1
					Taxiing_normal()
				}
			}
			else
			{
				Taxiing_normal()			
				
			}		
		}
		
		if (next_action === "End_of_Actions")
		{
			go_to_next_state(state_after_taxiing)
			first_action_set = false
		}
		
		// TODO: Think about better handling and protocol
		if (myAircraft.Get_Engine__failure() === true)
		{
			remove_me_and_my_aircraft("Engine__Failure during Taxiing")
		}		
	}
	
	
	
	
	//-----Instrument Panel(IP) Functions-----
	def Check_Instrument_Engine__RPM()
	{
		//println "Checking Engine RPM via instrument"
		return myAircraft.IP_Get_Engine__RPM()
	}
	def Check_Instrument_Engine__oil_pressure()
	{
		//println "Checking Engine oil pressure instrument"
		return myAircraft.IP_Get_Engine__oil_pressure()
	}
	
	def Check_Instrument_Engine__oil_temperature()
	{
		//println "Checking Engine oil temperature instrument"
		return myAircraft.IP_Get_Engine__oil_temperature()
	}
	
	def Check_Instrument_Aircraft__true_air_speed()
	{
		//println "Checking Aircraft Speed Instrument"
		return myAircraft.IP_Get_Aircraft__true_air_speed()
	}
	
	def Check_Instrument_Aircraft__pitch()
	{
		//println "Checking Aircraft pitch via instrument"
		return myAircraft.IP_Get_Aircraft__pitch()
	}
	
	def Check_Instrument_Aircraft__rate_of_climb()
	{
		//println "Checking Aircraft climb of rate via instrument"
		return myAircraft.IP_Get_Aircraft__rate_of_climb()
	}
	
	def Check_Instrument_Aircraft__height()
	{
		//println "Checking Aircraft height via instrument"
		return myAircraft.IP_Get_Aircraft__height()
	}
	
	def Check_Instrument_RWT__fuel_quantitity()
	{
		//println "Checking RWT fuel quantity via instrument"
		return myAircraft.IP_Get_RWT__fuel_quantity()
	}
	
	def Check_Instrument_LWT__fuel_quantitity()
	{
		//println "Checking LWT fuel quantity via instrument"
		return myAircraft.IP_Get_LWT__fuel_quantity()
	}
	
	// -----Other visual, feel and acoustic function-----
	
	//TODO: Remove this function and think about another solution, since the pilot CANT see the actual speed
	def Check_Visual_Aircraft__ground_speed_x()
	{
		//returns actual ground speed x
		return myAircraft.Get_Aircraft__ground_speed_x()
	}
	
	def Feel_Aircraft__acceleration_x()
	{
		// returns true if there is a forward acceleration
		if (myAircraft.Get_Aircraft__acceleration_x() > 0.2)
		{
			return true
		}
		else
		{
			return false
		}
	}
	
	def Feel_Aircraft__deceleration_x()
	{
		// returns true if there is a forward deceleration
		if (myAircraft.Get_Aircraft__acceleration_x() < -0.2)
		{
			return true
		}
		else
		{
			return false
		}
	}
	
	def Feel_Aircraft__acceleration_z()
	{
		// returns true if there is a climbing acceleration
		if (myAircraft.Get_Aircraft__acceleration_z() > 0.2)
		{
			return true
		}
		else
		{
			return false
		}
	}
	def Feel_Aircraft__deceleration_z()
	{
		// returns true if there is a falling acceleration
		if (myAircraft.Get_Aircraft__acceleration_z() < -0.2)
		{
			return true
		}
		else
		{
			return false
		}
	}
	
	def Hear_Aircraft__stall_sound()
	{
		// returns true if near or exceed stall
		return myAircraft.Get_Aircraft__stall_sound()
	}
	
	
	//-----Control_Input_Panel(CIP) Functions-----
	def Set_Aircraft__heading_bearing(input: real)
	{
		//println "Apply heading bearing " + input
		myAircraft.CIP_Set_Aircraft__heading_bearing(input)
	}
	
	def Apply_Engine__throttle(input : real)
	{
		//println "Apply throttle " + input
		myAircraft.CIP_Apply_Engine__throttle(input)
	}
	
	def Apply_Aircraft__pitch(input : real)
	{
		//println "Apply pitch (angle of attack):  " + input
		myAircraft.CIP_Apply_Aircraft__pitch(input)
	}
	
	def Set_Engine__ignition_switch(input : string)
	{
		//println "Set ignition switch to " + input
		myAircraft.CIP_Set_Engine__ignition_switch(input)
	}
	
	def Apply_Brake__deceleration(input : real)
	{
		//println "Apply Brake " + input
		myAircraft.CIP_Apply_Brake__deceleration(input)
	}
	
	def Set_Brake__parking_brake(input : string)
	{
		//println "Set Parking Brake to " + input
		myAircraft.CIP_Set_Brake__parking_brake(input)
	}
	
	def Set_Engine__mixture_control(input : string)
	{
		//println "Set Mixture Control"
		myAircraft.CIP_Set_Engine__mixture_control(input)
	}
	
	def Set_CIP__master_switch(input : string)
	{
		//println "Set Master Switch to " + input
		myAircraft.CIP_Set__master_switch(input)
	}
	
	
	
	//-----Visual Tire Functions------
	def Check_Visual_TireRightMainWheel__inflation()
	{	
		//println "Checking right Main Wheel Tire inflation"
		return myAircraft.Get_TireRightMainWheel__inflation()
	}
	
	def Check_Visual_TireLeftMainWheel__inflation()
	{	
		//println "Checking left Main Wheel Tire inflation"
		return myAircraft.Get_TireLeftMainWheel__inflation
	}
	
	def Check_Visual_TireNoseWheel__inflation()
	{	
		//println "Checking Nose Wheel Tire inflation"
		return myAircraft.Get_TireNoseWheel__inflation
	}
	
		
	//------Visual Fuel Tank Functions------
	// Note: Right - and LeftWingTank (RWT&LWT)
	def Check_Visual_RWT__fuel_quantity()
	{
		//println "Checking right wing tank fuel quantity"
		return myAircraft.Get_RWT__fuel_quantity()
	}
	
	def Check_Visual_LWT__fuel_quantity()
	{
		//println "Checking left wing tank fuel quantity"
		return myAircraft.Get_RWT__fuel_quantity()
	}
	
	def Check_Visual_RWT__water_sediments()
	{
		//println "Checking right wing tank for water sediments"
		return myAircraft.Get_RWT__water_sediments()
	}
	
	def Repair_Visual_RWT__water_sediments()
	{
		//println "Repairing right wing tank water sediments"
		myAircraft.Repair_RWT__water_sediments()
	}
	
	def Check_Visual_LWT__water_sediments()
	{
		//println "Checking left wing tank for water sediments"
		return myAircraft.Get_LWT__water_sediments()
	}
	
	def Repair_Visual_LWT__water_sediments()
	{
		//println "Repairing left wing tank water sediments"
		myAircraft.Repair_LWT__water_sediments()
	}
	
	//------Visual (or acoustic) Engine Functions------
	def Check_Visual_Engine__running()
	{
		//println "Checking Engine running"
		return myAircraft.Get_Engine__running()
	}
	
	def Check_Visual_Engine__oil()
	{
		//println "Checking Engine oil"
		return myAircraft.Get_Engine__oil()
	}
	
	def Refill_Visual_Engine__oil()
	{
		//println "Refill engine oil"
		myAircraft.Refill_Engine__oil()
	}
}



//-----Agent AirTrafficController sitting in tower and answering requests-----
agent AirTrafficController on AgentLayer
	{	
		initialize
		{
			initialize_general_values()	
			var x_spawn = 9.501334
			var y_spawn = 53.559712
			pos at #(x_spawn, y_spawn)
			request_approval = false
			available_runway_heading = airportstade.Get_available_runway_heading_list()
		}
		
		tick
		{
			if (state === "Listen_on_frequency")
			{
				timehandler.create_action_duration(2, 2, "None")
				if (timehandler.hold_action_time(timehandler.action_duration))
				{
					var temp_receiver = agentlayer.Listen_receiver_on_frequency()
					if (temp_receiver === identifier)
					{
						state = "Communicate_on_frequency"
						message_type_received = agentlayer.Listen_message_type_on_frequency()
						callsign_received = agentlayer.Listen_sender_identifier_on_frequency()
					}
				}
			}
			
			else if (state === "Communicate_on_frequency")
			{
				timehandler.create_action_duration(2, 2, "None")
				if (timehandler.hold_action_time(timehandler.action_duration))
				{
					//Calculation of runway heading
					//Initialization of calculation
					var wind_bearing = agentlayer.Get_Weather__wind_bearing()
					var temp_delta = 180.1
					var saved_heading : real
					//Looping through all available headings
					each (var temp_runway_heading in available_runway_heading)
					{
						if (formula.smallest_absolute_delta(temp_runway_heading, wind_bearing)< temp_delta)
						{
							saved_heading = temp_runway_heading
							temp_delta = formula.smallest_absolute_delta(temp_runway_heading, wind_bearing)
						}
					}
					runway_heading_calculated = saved_heading // TODO: Add information retrieval/calculation for heading
					
					
					if (message_type_received === "RequestTakeOffPreparationPoint")
					{
						taxipath = airportstade.Get_taxipath_to_TakeOffPreparationPoint(runway_heading_calculated)
						agentlayer.Communicate_answer_on_frequency(identifier, callsign_received, "AnswerTakeOffPreparationPoint", taxipath, runway_heading_calculated, request_approval)
					}
					else if(message_type_received === "RequestTakeOffHoldShortPoint")
					{
						taxipath = airportstade.Get_taxipath_to_RunwayHoldShortPoint(runway_heading_calculated)
						agentlayer.Communicate_answer_on_frequency(identifier, callsign_received, "AnswerTakeOffHoldShortPoint", taxipath, runway_heading_calculated, request_approval)
					}
					else if(message_type_received === "RequestTakeOff")
					{
						taxipath = airportstade.Get_taxipath_to_RunwayLineUpPoint(runway_heading_calculated)
						agentlayer.Communicate_answer_on_frequency(identifier, callsign_received, "AnswerTakeOff", taxipath, runway_heading_calculated, request_approval)
					}
					else if(message_type_received === "RequestLeavingAirpsace")
					{
						request_approval = true // always true so far
						agentlayer.Communicate_answer_on_frequency(identifier, callsign_received, "AnswerRequestAirspace", taxipath, runway_heading_calculated, request_approval)
					}
					else
					{
						agentlayer.Clear_frequency() // Tower always able to answer or Clear Frequency, when request insufficient		
					}
					reset_general_values()
				}
			}
		}
		
		//-----General Values and functions (Settings)-----
		observe var identifier : string  // only Tower ATC so far
		observe var state : string
		observe var message_type_received : string
		observe var callsign_received : string
		observe var runway_heading_calculated : real
		var timehandler = new TimeHandler()
		var airportstade = new AirportStade() // for airport information retrieval
		var taxipath : List<Tuple<real, real>>
		var request_approval : bool // either true or false for answering
		var available_runway_heading : List<real>
		var formula = new Formula()
		
		def initialize_general_values()
		{
			identifier = "Tower"
			state = "Listen_on_frequency"
			callsign_received = "None"
			message_type_received = "None"
			
		}
		
		def reset_general_values()
		{
			state = "Listen_on_frequency"
			callsign_received = "None"
			message_type_received = "None"
		}
		
	}



// TODO: Make a more convenient way for information retrieval of airport
// TODO: Adapt heading, so aircraft wont fall off the runway :D (needs to be like 107.5)
class AirportStade
{
	var taxipath: List<Tuple<real, real>>
	var available_runway_heading_list = #[109.0, 289.0]
	
	def Get_available_runway_heading_list() => return available_runway_heading_list
	
	def Get_taxipath_to_TakeOffPreparationPoint(heading : real)
	{
		if (heading === available_runway_heading_list.Get(0))
		{
			taxipath = #[#(9.4994287,53.5604441), #(9.4934248,53.5617509), #(9.4931937,53.5618008), #(9.4930632,53.5618664)]
			return taxipath
		}
		
		else if (heading === available_runway_heading_list.Get(1))
		{
			taxipath = #[#(9.4994287,53.5604441), #(9.5010603,53.5600816), #(9.5018283,53.559911), #(9.5041687,53.5594248), #(9.5043157,53.5594584)]
			return taxipath
		}
	}
	
	def Get_taxipath_to_RunwayHoldShortPoint(heading: real)
	{
		if (heading === available_runway_heading_list.Get(0))
		{
			taxipath = #[#(9.4930632,53.5618664),#(9.4930261,53.5619488),#(9.4930324,53.562018),#(9.4930903,53.5620665),#(9.4931671,53.5621049),#(9.4932789,53.5621333),#(9.493448,53.5621513),#(9.4936651,53.5621639),#(9.4939417,53.562171)]
			return taxipath
		}
		
		else if (heading === available_runway_heading_list.Get(1))
		{
			taxipath = #[#(9.5043157,53.5594584), #(9.5044877,53.5595613), #(9.5046714,53.5597239)]
			return taxipath
		}
	}
	
	def Get_taxipath_to_RunwayLineUpPoint(heading: real)
	{
		if (heading === available_runway_heading_list.Get(0))
		{
			taxipath = #[#(9.4939417,53.562171), #(9.494201,53.5621611), #(9.494446,53.5621252)]
			return taxipath
		}
		
		else if (heading === available_runway_heading_list.Get(1))
		{
			taxipath = #[#(9.5046714,53.5597239), #(9.5048527,53.5598626), #(9.5028175,53.5603097)]
			return taxipath
		}
	}
}

// -----Class with helper functions-----
class Formula
{
	def haversine(point1 : Tuple<real, real>, point2: Tuple<real, real>)
	{
		//Returns the distance between two points
		//Note: Math functions in Mars are using RADIANS [rad]
		//Input assumed: In GeoJson and Mars the order is (Lon[°], Lat[°])!
		var deg_to_rad_factor = Constants.Pi / 180
		var lon1 = point1.Item1() * deg_to_rad_factor
		var lat1 = point1.Item2() * deg_to_rad_factor
		var lon2 = point2.Item1() * deg_to_rad_factor
		var lat2 = point2.Item2() * deg_to_rad_factor
		
		var dlon = lon2 - lon1
		var dlat = lat2 - lat1
		
		//Workaround, since some problems with type appeared
		//a = sin(dlat/2)**2 + cos(lat1) * cos(lat2) * sin(dlon/2)**2
		var summand1 : real = (Math.Sin(dlat/2))**2
		var factor1 : real = Math.Cos(lat1)
		var factor2 : real = Math.Cos(lat2)
		var factor3 : real = (Math.Sin(dlon/2))**2
		var a = summand1 + (factor1 * factor2 * factor3)	
		
		//c = 2 * asin(sqrt(a)) 
		var c : real = 2 * Math.Atan2(a**0.5, (1-a)**0.5)
		var r = 6371000 //Radius of earth in meters
		
	    return c*r
		                                                                 
	}
	
	def bearing(point1 : Tuple<real, real>, point2: Tuple<real, real>)
	{
		// returns the bearing from point 1 TO point 2
		//Note: Math functions in Mars are using RADIANS [rad]
		//Input assumed: In GeoJson and Mars the order is (Lon[°], Lat[°])!
		var deg_to_rad_factor = Constants.Pi / 180
		var rad_to_deg_factor = 180 / Constants.Pi
		var lon1 = point1.Item1() * deg_to_rad_factor
		var lat1 = point1.Item2() * deg_to_rad_factor
		var lon2 = point2.Item1() * deg_to_rad_factor
		var lat2 = point2.Item2() * deg_to_rad_factor
		
		var dlon = lon2 - lon1
		
		//Workaround, since some problems with type appeared
		// bearing = atan2(sin(dlon) * cos(lat2), cos(lat1) * sin(lat2) - sin(lat1) * cos(lat2) * cos(dlon))
		var y : real = Math.Sin(dlon) * Math.Cos(lat2)
		var x1 : real = Math.Cos(lat1) * Math.Sin(lat2)
		var x2: real = Math.Sin(lat1) * Math.Cos(lat2)
		var x3 : real = Math.Cos(dlon)
		var x : real = x1 - x2 * x3
		
		var bearing : real = Math.Atan2(y, x) * rad_to_deg_factor
		bearing = (bearing + 360) % 360
		return bearing
	}
	
	def smallest_absolute_delta(a1 : real, a2: real)
	{
		//returns the smallest delta between 0..180 from two point in range from 0..360°
		var delta = 180 - Math.Abs(Math.Abs(a1 - a2)- 180)
		return delta
	}
	
}

// -----Class for handling all time issues-----
class TimeHandler
{
	//general values for timehandling
	var action_run_time : integer =  0
	var action_duration_time_set : bool = false
	var action_duration : integer
	
	// pilot characteristics for time calculations
	var pilot_age : integer
	var pilot_flight_experience: integer
	var pilot_age_max : integer
	var pilot_age_min : integer
	
	def initialize_variables
	(
		p_age : integer, 
		p_exp : integer,
		p_age_max : integer,
		p_age_min : integer
	)
	{
		pilot_age = p_age
		pilot_flight_experience = p_exp
		pilot_age_max = p_age_max
		pilot_age_min = p_age_min
	}
	
	def hold_action_time(time_needed : integer): bool 
	{
		action_run_time = action_run_time + 1
		if (action_run_time >= time_needed)
		{
			reset_action_timer()
			action_duration_time_set = false
			return true	
		}
		else
		{
			return false
		}
	}
	
	def reset_action_timer(): void
	{
		action_run_time = 0
	}
	
	def create_action_duration(action_duration_base : integer, action_duration_extra : integer, mode : string)
	{
		if (action_duration_time_set === false)
		{
			action_duration_time_set = true			
			if (mode === "pilot_age_and_experience")
			{
				var action_duration_extra_calc = action_duration_extra * (1 + (pilot_age / pilot_age_max) - pilot_flight_experience / (pilot_age_max - pilot_age_min) )
				action_duration = action_duration_base + random(action_duration_extra_calc)
			}
			else
			{
				action_duration = action_duration_base + random(action_duration_extra)
			}				
		}
	}
}



