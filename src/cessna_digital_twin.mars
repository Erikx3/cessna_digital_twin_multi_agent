model cessna_digital_twin

use Mars

layer AgentLayer as agentlayer
{
	
	//Weather Values TODO: Think about another solution for weather
	var Weather__density : real = 1.225 //kg/m^3 TODO: Add height dependent density
	def Get_Weather__density()
	{
		return Weather__density
	}
	
	
	//-----Spawn Handling-----
	var spawn_xcor : real = 9.4987928
	var spawn_ycor : real = 53.560392
	
	def Update_spawn_coord()
	{
		spawn_xcor = spawn_xcor + random(5) - 4
		spawn_ycor = spawn_ycor + random(5) - 4
	}	
	
	def Get_spawn_x_coord()
	{
		return spawn_xcor
	}	
	
	def Get_spawn_y_coord()
	{
		return spawn_ycor
	}
	
	
	
	//-----Communication Handling-----
	var message_on_frequency = false
	var sender_identifier = ""
	var receiver = ""
	var message_type = ""
	// either information is at first place "0" or contains multiple information as e.g. flight_path
	var message_information_path : List<Tuple<real, real>>
	
	def Listen_receiver_on_frequency() => return receiver
	def Listen_sender_identifier_on_frequency() => return sender_identifier
	def Listen_message_type_on_frequency() => return message_type
	def Listen_message_information_path_on_frequency() => return message_information_path 
	
	def Communicate_taxipath_on_frequency(_sender_identifier : string, _receiver : string, _message_type : string, _message_information_path  : List<Tuple<real, real>>)
	{
		if (message_on_frequency === false)
		{
			sender_identifier = _sender_identifier
			receiver = _receiver
			message_type = _message_type
			message_information_path  = _message_information_path 
			
			message_on_frequency = true
		}		
	}
	
	def Communicate_request_on_frequency(_sender_identifier : string, _receiver : string, _message_type : string)
	{
		if (message_on_frequency === false)
		{
			sender_identifier = _sender_identifier
			receiver = _receiver
			message_type = _message_type
			
			message_on_frequency = true
		}		
	}
	
	def Clear_frequency()
	{
		message_on_frequency = false
		sender_identifier = ""
		receiver = ""
		message_type = ""
		message_information_path = #[#(0.0, 0.0)]
	}
	
}

vector-layer AirportStadeLayer as airportstadelayer



agent Aircraft on AgentLayer{
	
	initialize
	{
		initialize_general_values()
		initialize_AircraftPhysics()
		initialize_RightWingTank()
		initialize_LeftWingTank()
		initialize_Tire()
		initialize_Engine()
		initialize_Brake()
		initialize_CIP()
	}
	
	tick
	{
		update_general_values()
		update_Engine()
	}
	
	
	
	// -----General Values and Functions-----
	observe var Latitude : real
	observe var Longitude : real
	observe var Callsign : string // IDENTIFIER!
	
	// TODO: Think about this for documentation
	//observe var passive_activity: string // activity by pilot is more precise
	//observe var malfunction_notification // maybe add something like this or think about it
	
	def initialize_general_values()
	{
		var x_spawn = agentlayer.Get_spawn_x_coord()
		var y_spawn = agentlayer.Get_spawn_y_coord()
		pos at #(x_spawn, y_spawn)
		
		Callsign = "Cessna" + random(10000) //TODO: Make sure, Callsign is Unique!
		Longitude = xcor
		Latitude = ycor
	}
	
	def update_general_values()
	{
		Longitude = xcor
		Latitude = ycor
	}
	
	passive Get_position() => return #(xcor, ycor) // for returning the position of the aircraft
	passive Get_callsign() => return Callsign
	
	
	//-----Aircraft(Movement and Physics)-----
	observe var Physics__speed : real //TODO: Think about cleaner naming scheme!!!!
	
	def initialize_AircraftPhysics()
	{
		Physics__speed = 0.0
	}
	
	// -----Propeller-----
	var Propeller__diameter : real = 1.75 // meters
	
	// -----Engine-----
	observe var Engine__oil : real  // litre
	observe var Engine__oil_pressure : real // normal between 205000 and 410000 Pa
	observe var Engine__oil_temperature : real // normal between 38째C and 116째C
	observe var Engine__mixture_control : real  // percentage RICH 0..1 LEAN
	observe var Engine__throttle : real  // percentage NO THROTTLE 0..1 FULL THROTTLE
	observe var Engine__power : real // between 0W and 75000W 
	observe var Engine__ignition_switch : string  // "OFF", "L", "R", "BOTH" or "START"
	observe var Engine__RPM : real  //calculated via formula with power coefficient
	observe var Engine__running : bool
	
	//Power Efficient Variable
	//Note: assumption cp = f(V/(n*d)) with n*d=1
	observe var Engine__power_coefficient : real
	var Engine__power_coefficient_slope : real // TODO: has to be initialized, since negative and Mars does not allow direct negative number assignment
	var Engine__power_coefficient_constant : real = 0.05
	var Engine__power_coefficient_speed_constant : real = 30.0 //Up to which speed coefficient is constant
	
	def Get_Engine__power_coefficient()
	{
		if (Physics__speed > Engine__power_coefficient_speed_constant)
		{
			Engine__power_coefficient = Engine__power_coefficient_slope * (Physics__speed - Engine__power_coefficient_speed_constant) + Engine__power_coefficient_constant
		}
		else
		{
			Engine__power_coefficient = Engine__power_coefficient_constant
		}
		return Engine__power_coefficient
	}
	
	
	//Settings
	var Engine__oil_max : integer = 6
	var Engine__oil_normal : integer = 5
	var Engine__oil_min : integer = 4
	var Engine__oil_pressure_normal_min = 205000
	var Engine__oil_pressure_normal_max = 410000
	var Engine__oil_pressure_min = 70000
	var Engine__oil_temperature_normal_min = 38
	var Engine__oil_temperature_normal_max = 116
	var Engine__RPM_max : integer = 2750
	var Engine__power_max : integer = 75000 // W
	
	//TODO, dependent on throttle settings!
	//observe var Engine__fuel_consumption : real
	
	//TODO Think about implementation
	//observe var Engine__oil_pump_condition : bool
	//observe var Engine__oil_pressure_gauge_condition : bool
	//observe var Engine__oil_leakage : real
	
	
	def initialize_Engine()
	{
		Engine__oil = 3.1 + random(3) + (random(10) / 10.0)
		Engine__mixture_control = random(101) / 100.0
		Engine__throttle = random(10) / 10.0
		Engine__ignition_switch = "OFF"
		Engine__RPM = 0.0
		Engine__power = 0.0
		Engine__running = false
		Engine__oil_pressure = 101325
		Engine__oil_temperature = 15
		
		Engine__power_coefficient_slope = -0.0009	
	}
	
	def Set_Engine__not_running_values()
	{
		Engine__RPM = 0.0
		Engine__oil_pressure = 101325
		Engine__oil_temperature = 15
	}
	
	def update_Engine()
	{
		if (Engine__running === false)
		{
			Set_Engine__not_running_values()
			if (Engine__ignition_switch === "START" and CIP__master_switch === "ON")
			{
				if (random(100) <= 40) // 40% chance for engine to start
				{
					println "The engine started successfully."
					Engine__running = true
				}
			}
		}
		
		if (Engine__running === true)
		{
			//Engine RPM Calculation TODO: Add speed dependency (and more..?), since RPM changes with aircraft speed see paper
			Engine__power = Engine__power_max * Engine__throttle
			Engine__power_coefficient = Get_Engine__power_coefficient()
			Engine__RPM = (Engine__power/(Engine__power_coefficient*agentlayer.Get_Weather__density()*Propeller__diameter**5))**0.3333 * 60 // FACTOR 60 TO CONVERT INTO PER MINUTE
			//Engine__RPM = Engine__RPM + random(21) - 10 // for random deviations
			
			//Engine Oil TODO: Make dependent on engine__oil_pump and possible leakage
			var temp_Engine__oil_pressure_normal_half = (Engine__oil_pressure_normal_max + Engine__oil_pressure_normal_min) / 2
			var temp_Engine__oil_temperature_normal_half = (Engine__oil_temperature_normal_max + Engine__oil_temperature_normal_min) / 2
			Engine__oil_pressure = temp_Engine__oil_pressure_normal_half // Normal Operations
			Engine__oil_temperature = temp_Engine__oil_temperature_normal_half //TODO: maybe add dependence to throttle here at least
			if (Engine__oil < Engine__oil_normal)
			{
				//TODO: Add effect on Engine failure probability
				Engine__oil_pressure = Engine__oil_pressure - ((temp_Engine__oil_pressure_normal_half - Engine__oil_pressure_normal_min) * (Engine__oil_normal - Engine__oil))
				Engine__oil_temperature = Engine__oil_temperature + ((Engine__oil_temperature_normal_max - temp_Engine__oil_temperature_normal_half) * (Engine__oil_normal - Engine__oil))
			}
			
			
		}
	}
	
	passive Get_Engine__running() => return Engine__running
	passive Get_Engine__oil() => return Engine__oil
	
	
	
	// -----Right Wing Tank (RWT)-----
	// Note: Right - and LeftWingTank (RWT&LWT)
	observe var RWT__fuel_quantity : real 
	observe var RWT__water_sediments : bool
	var RWT__total_capacity : integer = 49 // Litre
		
	passive Get_RWT__fuel_quantity() => return RWT__fuel_quantity
	passive Get_RWT__water_sediments() => return RWT__water_sediments
	
	def initialize_RightWingTank()
	{
		RWT__fuel_quantity = random(RWT__total_capacity+1)
		RWT__water_sediments = false
	}
	
	// -----Left Wing Tank (LWT)-----
	observe var LWT__fuel_quantity : real // Litre
	observe var LWT__water_sediments : bool
	var LWT__total_capacity : integer = 49 // Litre
	
	passive Get_LWT__fuel_quantity() => return LWT__fuel_quantity // return of fuel quantity "visual"
	passive Get_LWT__water_sediments() => return LWT__water_sediments
	
	def initialize_LeftWingTank()
	{
		LWT__fuel_quantity = random(LWT__total_capacity+1)
		LWT__water_sediments = false
	}
	
	
	
	
	// -----Tire-----
	observe var TireRightMainWheel__inflation : integer
	observe var TireLeftMainWheel__inflation : integer
	observe var TireNoseWheel__inflation : integer
	var TireRightMainWheel__inflation_max : integer = 145000
	var TireLeftMainWheel__inflation_max : integer = 145000
	var TireNoseWheel__inflation_max : integer = 207000
	
	// 1% Chance that a Tire is not fully inflated
	def initialize_Tire()
	{
		if (random(100) <= 99)
		{
			TireRightMainWheel__inflation = TireRightMainWheel__inflation_max
		}
		else
		{
			TireRightMainWheel__inflation = random(TireRightMainWheel__inflation_max)
		}
		
		if (random(100) <= 99)
		{
			TireLeftMainWheel__inflation = TireLeftMainWheel__inflation_max
		}
		else
		{
			TireLeftMainWheel__inflation = random(TireLeftMainWheel__inflation_max)
		}
		
		if (random(100) <= 99)
		{
			TireNoseWheel__inflation = TireNoseWheel__inflation_max
		}
		else
		{
			TireNoseWheel__inflation = random(TireNoseWheel__inflation_max)
		}
	}
	
	passive Get_TireRightMainWheel__inflation() => return TireRightMainWheel__inflation
	passive Get_TireLeftMainWheel__inflation() => return TireLeftMainWheel__inflation
	passive Get_TireNoseWheel__inflation() => return TireNoseWheel__inflation
	
	
	//-----Brake System-----
	observe var Brake__parking_brake : string // "SET" or "OFF"
	
	def initialize_Brake()
	{
		Brake__parking_brake = "SET" 
	}
	
	
	
	//-----Control_Input_Panel(CIP)-----
	// Interface for pilot input when sitting in Aircraft for giving Input
	
	// master switch here, since placing in other classes difficult
	observe var CIP__master_switch : string // "ON" or "OFF"
	
	def initialize_CIP()
	{
		CIP__master_switch = "OFF" 
	}
	
	passive CIP_Set_Engine__ignition_switch(input : string)
	{
		Engine__ignition_switch = input
	}

	passive CIP_Apply_Engine__throttle(input: real)
	{
		Engine__throttle = input
		
		// redundancy
		if (Engine__throttle < 0)
		{
			Engine__throttle = 0
		}
		else if (Engine__throttle > 1)
		{
			Engine__throttle = 1
		}
	}
	
	passive CIP_Set__master_switch(input: string)
	{
		CIP__master_switch = input
	}	
	
	passive CIP_Set_Brake__parking_brake(input : string)
	{
		Brake__parking_brake = input
	}
	
	passive CIP_Set_Engine__mixture_control(input : real)
	{
		Engine__mixture_control = input
	}
	
	
	
	//-----Instrument_Panel(IP)-----
	// Interface for pilot input when sitting in Aircraft for reading Instruments
	
	passive IP_Get_Engine__oil_pressure()
	{
		return Engine__oil_pressure()
	}
	
	passive IP_Get_Engine__oil_temperature()
	{
		return Engine__oil_temperature()
	}
}



// -----Agent Pilot flying the aircraft-----
agent Pilot on AgentLayer{
		
	initialize
	{
		// Spawn
		var x_spawn = agentlayer.Get_spawn_x_coord()
		var y_spawn = agentlayer.Get_spawn_y_coord()
		pos at #(x_spawn, y_spawn)
		
		//General Initialization
		//TODO: Add redundancy for making sure, not two pilots controlling 1 Aircraft
		myAircraft = nearest Aircraft // assign aircraft only ones
		myAircraft_callsign = myAircraft.Get_callsign() // save aircraft callsign --> IDENTIFIER!
		update_general_values() // so first row of csv is not empty
		state = "PreflightInspection" // first state so far
		
		// Pilot attributes and share them
		age = age_min + random((age_max - age_min) + 1)
		flight_experience = random(age - age_min) +1
		timehandler.initialize_variables(age, flight_experience, age_max, age_min)
	}
		
	tick
	{	
		if (state === "PreflightInspection"){
			PreflightInspection_action()
		}
		
		else if (state === "StartingEngine"){
			StartingEngine_action()
		}
		
		else if (state === "TakeOffPreparationRequest"){
			TakeOffPreparationRequest_action()
		}
		
		else if (state === "Taxiing"){
			Taxiing_action()
		}
		
		update_general_values()
	}
	
	
	//-----General Values and functions (Settings)-----
	observe var Latitude : real
	observe var Longitude : real
	observe var myAircraft_callsign : string
	observe var state : string
	observe var current_activity : string
	
	var myAircraft : Aircraft // for accessing the real aircraft
	var timehandler = new TimeHandler()
	var formula = new Formula()
	var first_action_set : bool = false // temporary auxiliary variable
	var taxi_path : List<Tuple<real, real>> // for saving taxi instructions
	
	var next_action : string
	var age_max : integer = 75
	var age_min : integer = 18
	
	def go_to_next_state(_state : string)
	{
		state = _state
		first_action_set = false
	}
	
	def update_general_values()
	{
		pos at(myAircraft.Get_position())
		Longitude = xcor
		Latitude = ycor
		current_activity = next_action
	}
	
	
	//-----General Pilot Values and functions-----
	observe var age: integer // years
	observe var flight_experience : integer // years
		
	

		
	
	
	//-----State Preflight Inspection-----
	// ---TODO: Add variable outcomes!---
	def PreflightInspection_action()
	{
		// Flag for first action and architecture like this avoids loose of one tick
		if (first_action_set === false)
		{
			next_action = "Check_TireRightMainWheel__inflation"
			first_action_set = true
		}
		
		if (next_action === "Check_TireRightMainWheel__inflation")
		{
			timehandler.create_action_duration(10, 10, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_TireRightMainWheel__inflation()
				if (check_value >= 0)
				{
					next_action = "Check_RWT__water_sediments"
				}
			}
			
		}		
		
		else if (next_action === "Check_RWT__water_sediments")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_RWT__water_sediments()
				if (check_value === true or check_value === false) 
				{
					next_action = "Check_RWT__fuel_quantity"
				}
			}
			
		}
		
		else if (next_action === "Check_RWT__fuel_quantity")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_RWT__fuel_quantity()
				if (check_value >= 0) 
				{
					next_action = "Check_Engine__oil"
				}
			}
		}
				
		else if (next_action === "Check_Engine__oil")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_Engine__oil()
				if (check_value >= 0) 
				{
					next_action = "Check_TireNoseWheel__inflation"
				}
			}
		}
		
		else if (next_action === "Check_TireNoseWheel__inflation")
		{
			timehandler.create_action_duration(10, 10, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_TireNoseWheel__inflation()
				if (check_value >= 0) 
				{
					next_action = "Check_TireLeftMainWheel__inflation"
				}
			}
		}
		
		else if (next_action === "Check_TireLeftMainWheel__inflation")
		{
			timehandler.create_action_duration(10, 10, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_TireLeftMainWheel__inflation()
				if (check_value >= 0) 
				{
					next_action = "Check_LWT__water_sediments"
				}
			}
		}				
		
		else if(next_action === "Check_LWT__water_sediments")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_LWT__water_sediments()
				if (check_value === true or check_value === false)
				{
					next_action = "Check_LWT__fuel_quantity"
				}
			}
		}					
					
		else if (next_action === "Check_LWT__fuel_quantity")
		{
			timehandler.create_action_duration(20, 20, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_LWT__fuel_quantity()
				if (check_value >= 0) 
				{
					next_action = "End_of_Actions"
				}
			}
		}
			
		if (next_action === "End_of_Actions")
		{
			println "Finished State -->" + state + " with next action flag -->" + next_action
			go_to_next_state("StartingEngine")
			println "Next state -->" + state
		}
	}
	
	
	
	//-----State Starting Engine-----
	def StartingEngine_action()
	{
		if (first_action_set === false)
		{
			next_action = "Set_Brake__parking_brake"
			first_action_set = true
		}
		
		if (next_action === "Set_Brake__parking_brake")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Brake__parking_brake("SET")
				next_action = "Set_Engine__mixture_control"
			}
		}
		
		else if (next_action === "Set_Engine__mixture_control")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Engine__mixture_control(0) // set to rich
				next_action = "Set_CIP__master_switch"
			}
		}
		
		else if (next_action === "Set_CIP__master_switch")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_CIP__master_switch("ON")
				next_action = "Apply_Engine__throttle"
			}
		}
		
		else if (next_action === "Apply_Engine__throttle")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var throttle_value = 0.05 + (random(11) / 100.0) // random idle value
				Apply_Engine__throttle(throttle_value)
				next_action = "Set_Engine__ignition_switch_START"
			}
		}
		
		else if (next_action === "Set_Engine__ignition_switch_START")
		{
			timehandler.create_action_duration(4, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Engine__ignition_switch("START")
				next_action = "Check_Engine__running"
			}
		}
		
		else if (next_action === "Check_Engine__running")
		{
			timehandler.create_action_duration(1, 0, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				var check_value = Check_Visual_Engine__running()
				if (check_value === true)
				{
					next_action = "Set_Engine__ignition_switch_BOTH"
				}
				else if (check_value === false)
				{
					next_action = "Check_Engine__running"
				}
			}
		}
			
		else if (next_action === "Set_Engine__ignition_switch_BOTH")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Engine__ignition_switch("BOTH")
				next_action = "Check_Instrument_Engine__oil_pressure"
			}
		}
		
		else if (next_action === "Check_Instrument_Engine__oil_pressure")
		{
			timehandler.create_action_duration(30, 2, "pilot_age_and_experience") // includes holding pattern
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Check_Instrument_Engine__oil_pressure()
				next_action = "Check_Instrument_Engine__oil_temperature"
			}
		}
		
		else if (next_action === "Check_Instrument_Engine__oil_temperature")
		{
			timehandler.create_action_duration(3, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Check_Instrument_Engine__oil_temperature()
				next_action = "End_of_Actions"
			}
		}
		
		if (next_action === "End_of_Actions")
		{
			println "Finished State -->" + state + " with next action flag -->" + next_action
			go_to_next_state("TakeOffPreparationRequest")
			println "Next state -->" + state
		}
		
	}
	
	
	
	
	//-----State TakeOffPreparationRequest-----
	def TakeOffPreparationRequest_action()
	{
		if (first_action_set === false)
		{
			next_action = "Set_Brake__parking_brake"
			first_action_set = true
		}
		
		if (next_action === "Set_Brake__parking_brake")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Communicate_request_on_frequency(myAircraft_callsign, "Tower", "RequestTakeOffPreparationPoint")
				println "Communicating on frequency"
				next_action = "Listen_receiver_on_frequency"
			}
		}
		
		else if (next_action === "Listen_receiver_on_frequency")
		{
			var temp_receiver = agentlayer.Listen_receiver_on_frequency()
			if (temp_receiver === myAircraft_callsign)
			{
				taxi_path = agentlayer.Listen_message_information_path_on_frequency()
				next_action = "Clear_frequency"
				println "Listen on frequency successful"
			}
			
			//retry contacting tower after a specific period of time
			timehandler.create_action_duration(10, 5, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				next_action = "Communicate_on_frequency"
			}
		}
		
		// to make sure that message is not instantly deleted and simulate processing time of incoming message and blockage
		else if (next_action === "Clear_frequency")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				agentlayer.Clear_frequency()
				println "Clear on frequency successful"
				next_action = "End_of_Actions"
			}
		}
		
		if (next_action === "End_of_Actions")
		{
			println "Finished State -->" + state + " with next action flag -->" + next_action
			go_to_next_state("Taxiing")
			println "Next state -->" + state
		}
	}
	
	
	//-----State Taxiing in general-----
	// TODO: Important Note: Before entering Taxi State, define the "normal" State after Taxiing
	//help variable:
	var active_taxi_point_number : integer
	def Taxiing_action()
	{
		var taxi_path_points = taxi_path.Size()
		if (first_action_set === false)
		{
			next_action = "Set_Brake__parking_brake"
			first_action_set = true
		}
		
		if (next_action === "Set_Brake__parking_brake")
		{
			timehandler.create_action_duration(2, 2, "pilot_age_and_experience")
			if (timehandler.hold_action_time(timehandler.action_duration))
			{
				Set_Brake__parking_brake("OFF")
				println "Set braking park"
				next_action = "Taxiing"
			}
		}
		
		else if (next_action === "Taxiing")
		{
			var active_taxi_point = taxi_path.Get(active_taxi_point_number)
			
			move myAircraft 5 to active_taxi_point
			
			println "-----Taxiing-----"
			println "taxi_path_points : " + taxi_path_points
			println "active_taxi_point_number : " + active_taxi_point_number
			println "My Position : "
			println myAircraft.Get_position()
			println "active_taxi_point : "
			println active_taxi_point
			
			var distance_to_next_point = formula.haversine(myAircraft.Get_position(), active_taxi_point)
			println "distance: " + distance_to_next_point
			
			if (distance_to_next_point < 10)
			{
				if (taxi_path_points === (active_taxi_point_number + 1))
				{
					next_action = "End_of_Actions"				
				}
				else
				{
					active_taxi_point_number = active_taxi_point_number + 1
				}
			}
			
		}
		
		if (next_action === "End_of_Actions")
		{
			println "Finished State -->" + state + " with next action flag -->" + next_action
			state = "NextStateTaxiingIGuess"
			println "Next state -->" + state
			first_action_set = false
		}	
	}
	
	
	
	
	//-----Instrument Panel(IP) Functions-----
	def Check_Instrument_Engine__oil_pressure()
	{
		println "Checking Engine oil pressure instrument"
		return myAircraft.IP_Get_Engine__oil_pressure()
	}
	
	def Check_Instrument_Engine__oil_temperature()
	{
		println "Checking Engine oil temperature instrument"
		return myAircraft.IP_Get_Engine__oil_temperature()
	}
	
	
	
	//-----Control_Input_Panel(CIP) Functions-----
	def Apply_Engine__throttle(input : real)
	{
		println "Apply throttle"
		myAircraft.CIP_Apply_Engine__throttle(input)
	}
	
	def Set_Engine__ignition_switch(input : string)
	{
		println "Set ignition switch to " + input
		myAircraft.CIP_Set_Engine__ignition_switch(input)
	}
	
	
	def Set_Brake__parking_brake(input : string)
	{
		println "Set Parking Brake to " + input
		myAircraft.CIP_Set_Brake__parking_brake(input)
	}
	
	def Set_Engine__mixture_control(input : real)
	{
		println "Set Mixture Control"
		myAircraft.CIP_Set_Engine__mixture_control(input)
	}
	
	def Set_CIP__master_switch(input : string)
	{
		println "Set Master Switch to " + input
		myAircraft.CIP_Set__master_switch(input)
	}
	
	//-----Visual Tire Functions------
	def Check_Visual_TireRightMainWheel__inflation()
	{	
		println "Checking right Main Wheel Tire inflation"
		return myAircraft.Get_TireRightMainWheel__inflation()
	}
	
	def Check_Visual_TireLeftMainWheel__inflation()
	{	
		println "Checking left Main Wheel Tire inflation"
		return myAircraft.Get_TireLeftMainWheel__inflation
	}
	
	def Check_Visual_TireNoseWheel__inflation()
	{	
		println "Checking Nose Wheel Tire inflation"
		return myAircraft.Get_TireNoseWheel__inflation
	}
	
		
	//------Visual Fuel Tank Functions------
	// Note: Right - and LeftWingTank (RWT&LWT)
	def Check_Visual_RWT__fuel_quantity()
	{
		println "Checking right wing tank fuel quantity"
		return myAircraft.Get_RWT__fuel_quantity()
	}
	
	def Check_Visual_LWT__fuel_quantity()
	{
		println "Checking left wing tank fuel quantity"
		return myAircraft.Get_RWT__fuel_quantity()
	}
	
	def Check_Visual_RWT__water_sediments()
	{
		println "Checking right wing tank for water sediments"
		return myAircraft.Get_RWT__water_sediments()
	}
	
	def Check_Visual_LWT__water_sediments()
	{
		println "Checking left wing tank for water sediments"
		return myAircraft.Get_LWT__water_sediments()
	}
	
	//------Visual (or acoustic) Engine Functions------
	def Check_Visual_Engine__running()
	{
		println "Checking Engine running"
		return myAircraft.Get_Engine__running()
	}
	
	def Check_Visual_Engine__oil()
	{
		println "Checking Engine oil"
		return myAircraft.Get_Engine__oil()
	}
	
}




//-----Agent AirTrafficController sitting in tower and answering requests-----
agent AirTrafficController on AgentLayer
	{	
		initialize
		{
			initialize_general_values()
			// Spawn TODO: Tower Coordinaten und ihn im Kreis laufen lassen ;D
			var x_spawn = agentlayer.Get_spawn_x_coord()
			var y_spawn = agentlayer.Get_spawn_y_coord()
			pos at #(x_spawn, y_spawn)
		}
		
		tick
		{
			if (state === "Listen_on_frequency")
			{
				timehandler.create_action_duration(2, 2, "None")
				if (timehandler.hold_action_time(timehandler.action_duration))
				{
					var temp_receiver = agentlayer.Listen_receiver_on_frequency()
					if (temp_receiver === identifier)
					{
						state = "Communicate_on_frequency"
						message_type_received = agentlayer.Listen_message_type_on_frequency()
						callsign_received = agentlayer.Listen_sender_identifier_on_frequency()
					}
				}
			}
			
			else if (state === "Communicate_on_frequency")
			{
				timehandler.create_action_duration(2, 2, "None")
				if (timehandler.hold_action_time(timehandler.action_duration))
				{
					agentlayer.Clear_frequency() // Tower always able to answer or Clear Frequency, when request insufficient	
					runway_heading_calculated = 107 // TODO: Add information retrieval for heading
					
					if (message_type_received === "RequestTakeOffPreparationPoint")
					{
						taxipath = airportstade.Get_taxipath_to_TakeOffPreparationPoint(runway_heading_calculated)
						agentlayer.Communicate_taxipath_on_frequency(identifier, callsign_received, "AnswerTakeOffPreparationPoint", taxipath)
						state = "Listen_on_frequency"
					}
				}
			}
		}
		
		//-----General Values and functions (Settings)-----
		observe var identifier : string  // only Tower ATC so far
		observe var state : string
		observe var message_type_received : string
		observe var callsign_received : string
		observe var runway_heading_calculated : integer
		var timehandler = new TimeHandler()
		var airportstade = new AirportStade() // for airport information retrieval
		var taxipath : List<Tuple<real, real>>
		
		def initialize_general_values()
		{
			identifier = "Tower"
			state = "Listen_on_frequency"
		}
		
	}


// TODO: Make a more convenient way for information retrieval of airport
class AirportStade
{
	var taxipath: List<Tuple<real, real>>
	
	def Get_taxipath_to_TakeOffPreparationPoint(heading : integer)
	{
		if (heading === 107)
		{
			taxipath = #[#(9.4994287,53.5604441), #(9.4934248,53.5617509), #(9.4931937,53.5618008), #(9.4930632,53.5618664)]
			return taxipath
		}
		
		else if (heading === 287)
		{
			taxipath = #[#(9.4994287,53.5604441), #(9.5010603,53.5600816), #(9.5018283,53.559911), #(9.5041687,53.5594248), #(9.5043157,53.5594584)]
			return taxipath
		}
	}
	
	def Get_taxipath_to_RunwayHoldShortPoint(heading: integer)
	{
		if (heading === 107)
		{
			taxipath = #[#(9.4930632,53.5618664),#(9.4930261,53.5619488),#(9.4930324,53.562018),#(9.4930903,53.5620665),#(9.4931671,53.5621049),#(9.4932789,53.5621333),#(9.493448,53.5621513),#(9.4936651,53.5621639),#(9.4939417,53.562171)]
			return taxipath
		}
		
		else if (heading === 287)
		{
			taxipath = #[#(9.5043157,53.5594584), #(9.5044877,53.5595613), #(9.5046714,53.5597239)]
			return taxipath
		}
	}
	
	def Get_taxipath_to_RunwayLineUpPoint(heading: integer)
	{
		if (heading === 107)
		{
			taxipath = #[#(9.4939417,53.562171), #(9.494201,53.5621611), #(9.494446,53.5621252)]
			return taxipath
		}
		
		else if (heading === 287)
		{
			taxipath = #[#(9.5046714,53.5597239), #(9.5048527,53.5598626), #(9.5028175,53.5603097)]
			return taxipath
		}
	}
}

// -----Class with helper functions-----
class Formula
{
	def haversine(point1 : Tuple<real, real>, point2: Tuple<real, real>)
	{
		//Returns the distance between two points
		//Note: Math functions in Mars are using RADIANS [rad]
		//Input assumed: In GeoJson and Mars the order is (Lon[째], Lat[째])!
		var rad_factor = Constants.Pi / 180
		var lon1 = point1.Item1() * rad_factor
		var lat1 = point1.Item2() * rad_factor
		var lon2 = point2.Item1() * rad_factor
		var lat2 = point2.Item2() * rad_factor
		
		var dlon = lon2 - lon1
		var dlat = lat2 - lat1
		
		//Workaround, since some problems with type appeared
		//a = sin(dlat/2)**2 + cos(lat1) * cos(lat2) * sin(dlon/2)**2
		var summand1 : real = (Math.Sin(dlat/2))**2
		var factor1 : real = Math.Cos(lat1)
		var factor2 : real = Math.Cos(lat2)
		var factor3 : real = (Math.Sin(dlon/2))**2
		var a = summand1 + (factor1 * factor2 * factor3)	
		
		//c = 2 * asin(sqrt(a)) 
		var c : real = 2 * Math.Asin(a**0.5)
		
		var r = 6371000 //Radius of earth in meters
		
	    return c*r
		                                                                 
	}
}

// -----Class for handling all time issues-----
class TimeHandler
{
	//general values for timehandling
	var action_run_time : integer =  0
	var action_duration_time_set : bool = false
	var action_duration : integer
	
	// pilot characteristics for time calculations
	var pilot_age : integer
	var pilot_flight_experience: integer
	var pilot_age_max : integer
	var pilot_age_min : integer
	
	def initialize_variables
	(
		p_age : integer, 
		p_exp : integer,
		p_age_max : integer,
		p_age_min : integer
	)
	{
		pilot_age = p_age
		pilot_flight_experience = p_exp
		pilot_age_max = p_age_max
		pilot_age_min = p_age_min
	}
	
	def hold_action_time(time_needed : integer): bool 
	{
		action_run_time = action_run_time + 1
		if (action_run_time >= time_needed)
		{
			reset_action_timer()
			action_duration_time_set = false
			return true	
		}
		else
		{
			return false
		}
	}
	
	def reset_action_timer(): void
	{
		action_run_time = 0
	}
	
	def create_action_duration(action_duration_base : integer, action_duration_extra : integer, mode : string)
	{
		if (action_duration_time_set === false)
		{
			action_duration_time_set = true			
			if (mode === "pilot_age_and_experience")
			{
				var action_duration_extra_calc = action_duration_extra * (1 + (pilot_age / pilot_age_max) - pilot_flight_experience / (pilot_age_max - pilot_age_min) )
				action_duration = action_duration_base + random(action_duration_extra_calc)
			}
			else
			{
				action_duration = action_duration_base + random(action_duration_extra)
			}				
		}
	}
}
